; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "NoMercy"
#define MyAppVersion "1.0"
#define MyAppPublisher "NoMercy.ac"
#define MyAppURL "https://www.nomercy.ac/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{65EB0EE3-EF5A-44FF-A2B5-A2D0E8E1D114}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
VersionInfoVersion={#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputBaseFilename=NoMercySetup
SetupIconFile={#SourcePath}\..\..\source\Core\EngineR3_Core\res\icon_image.ico
Compression=lzma2
SolidCompression=yes
WizardStyle=modern
ArchitecturesInstallIn64BitMode=x64
CloseApplications=force

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
; Place all x64 files here
Source: "{#SourcePath}\..\bin\CrashHandler\NoMercy_Crash_Handler_x64.exe"; DestDir: "{app}"; Flags: ignoreversion; Check: Is64BitInstallMode
Source: "{#SourcePath}\..\..\Bin\NoMercy_Module_x64.dll"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
Source: "{#SourcePath}\..\..\Bin\NoMercy_Service_x64.exe"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
Source: "{#SourcePath}\..\..\Bin\NoMercy_Telemetry_x64.exe"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
Source: "{#SourcePath}\..\..\Bin\NoMercy_SystemModule_x64.sys"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
; Place all x86 files here, first one should be marked 'solidbreak'
; ...
; Place all common files here, first one should be marked 'solidbreak'
Source: "{#SourcePath}\..\bin\CrashHandler\NoMercy_Crash_Handler_x86.exe"; DestDir: "{app}"; Flags: ignoreversion solidbreak
Source: "{#SourcePath}\..\..\Bin\NoMercy_Module_x86.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\..\..\Bin\NoMercy_Service_x86.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\..\..\Bin\NoMercy_Telemetry_x86.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\..\..\Bin\NoMercy_SystemModule_x86.sys"; DestDir: "{app}"; Flags: ignoreversion skipifsourcedoesntexist
Source: "{#SourcePath}\..\..\Bin\NoMercy_Uninstall.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\..\..\Bin\SystemHealthCheck.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#SourcePath}\..\..\Bin\UpdatePatchHelper.exe"; DestDir: "{app}"; Flags: ignoreversion     
Source: "{#SourcePath}\..\..\Bin\NoMercy.cdb"; DestDir: "{app}\Data"; Flags: ignoreversion
Source: "{#SourcePath}\..\..\Bin\NoMercy.dat"; DestDir: "{app}\Data"; Flags: ignoreversion
Source: "{#SourcePath}\..\..\document\i18n_files\*"; DestDir: "{app}\I18n"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#SourcePath}\..\..\document\license_files\*"; DestDir: "{app}\License"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"

[Code]

{ ///////////////////////////////////////////////////////////////////// }
function GetUninstallString(): String;
var
  sUnInstPath: String;
  sUnInstallString: String;
begin
  sUnInstPath := ExpandConstant('Software\Microsoft\Windows\CurrentVersion\Uninstall\{#emit SetupSetting("AppId")}_is1');
  sUnInstallString := '';
  if not RegQueryStringValue(HKLM, sUnInstPath, 'UninstallString', sUnInstallString) then
    RegQueryStringValue(HKCU, sUnInstPath, 'UninstallString', sUnInstallString);
  Result := sUnInstallString;
end;


{ ///////////////////////////////////////////////////////////////////// }
function IsUpgrade(): Boolean;
begin
  Result := (GetUninstallString() <> '');
end;


{ ///////////////////////////////////////////////////////////////////// }
function UnInstallOldVersion(): Integer;
var
  sUnInstallString: String;
  iResultCode: Integer;
begin
{ Return Values: }
{ 1 - uninstall string is empty }
{ 2 - error executing the UnInstallString }
{ 3 - successfully executed the UnInstallString }

  { default return value }
  Result := 0;

  { get the uninstall string of the old app }
  sUnInstallString := GetUninstallString();
  if sUnInstallString <> '' then begin
    sUnInstallString := RemoveQuotes(sUnInstallString);
    if Exec(sUnInstallString, '/SILENT /NORESTART /SUPPRESSMSGBOXES','', SW_HIDE, ewWaitUntilTerminated, iResultCode) then
      Result := 3
    else
      Result := 2;
  end else
    Result := 1;
end;

{ ///////////////////////////////////////////////////////////////////// }
procedure CurStepChanged(CurStep: TSetupStep);
begin
  if (CurStep=ssInstall) then
  begin
    if (IsUpgrade()) then
    begin
      UnInstallOldVersion();
    end;
  end;
end;
